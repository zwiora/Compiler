#[ Rozklad liczby 340282367713220089251654026161790386200 na czynniki pierwsze ]
#[
#  Oczekiwany wynik:
#  2^3
#  3
#  5^2
#  7
#  13
#  41
#  61
#  641
#  1321
#  6700417
#  613566757
#  715827883
#]
PROGRAM IS
    a[4],
    n, m, reszta, potega, dzielnik
IN
    a[0] := 4294967297;
    a[1] := 4294967298;
    a[2] := 4294967299;
    a[3] := 4294967300;

    n := a[0] * a[1];
    n :=   n  * a[2];
    n :=   n  * a[3];

    dzielnik := 2;
    m := dzielnik * dzielnik;
    WHILE n >= m DO
        potega := 0;
        reszta := n % dzielnik;
        WHILE reszta = 0 DO
            n := n / dzielnik;
            potega := potega + 1;
            reszta := n % dzielnik;
        ENDWHILE
        IF potega > 0 THEN # [ czy znaleziono dzielnik ]
            WRITE dzielnik;
            WRITE potega;
        ELSE
            dzielnik := dzielnik + 1;
            m := dzielnik * dzielnik;
        ENDIF
    ENDWHILE
    IF n != 1 THEN # [ ostatni dzielnik ]
        WRITE n;
        WRITE 1;
    ENDIF
END
